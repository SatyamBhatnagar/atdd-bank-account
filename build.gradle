defaultTasks 'clean', 'build'

apply plugin: 'java'

wrapper {
    gradleVersion = "4.8.1"
}

repositories {
    mavenCentral()
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    test {
        java.srcDirs = ['src/test/unit/java']
        resources.srcDirs = ['src/test/unit/resources']
    }
    acceptanceTest {
        java.srcDirs = ['src/test/acceptance/java']
        resources.srcDirs = ['src/test/acceptance/resources']
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output
    }
}

configurations {
    acceptanceTest.extendsFrom testCompile
    acceptanceTest.extendsFrom testRuntime
}

dependencies {
    testCompile 'org.junit.jupiter:junit-jupiter:5.4.0'
    testCompile 'org.hamcrest:hamcrest-core:2.1'
    acceptanceTestCompile 'org.concordion:concordion:2.2.0'
}

test {
    systemProperty "user.country", "EN"
    systemProperty "user.language", "en"
    outputs.upToDateWhen { false }
    useJUnitPlatform()
}

task acceptanceTest(type: Test) {
    group = LifecycleBasePlugin.VERIFICATION_GROUP
    description = 'Runs the acceptance tests.'
    testClassesDirs = sourceSets.acceptanceTest.output.classesDirs
    classpath = sourceSets.acceptanceTest.runtimeClasspath
    outputs.upToDateWhen { false }
    systemProperties['concordion.output.dir'] = "$reporting.baseDir/spec"
    testLogging.showStandardStreams = true
}

check.dependsOn acceptanceTest
acceptanceTest.mustRunAfter test
